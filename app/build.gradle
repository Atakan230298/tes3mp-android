apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.bugsnag.android.gradle'

ext {
    versionFile = new File(project.rootDir, 'app/src/main/assets/libopenmw/resources/version')
    calculateVersion = {
        stream = new FileInputStream(versionFile)
        return stream.readLines()[0].trim() + "-" + project.android.defaultConfig.versionCode.toString()
    }
}

android {
    compileSdkVersion 28

    sourceSets {
        main {
            resources {
                srcDir {
                    // To include the wrap.sh script required by ASAN
                    "wrap/res"
                }
            }
        }
    }

    defaultConfig {
        applicationId "is.terrabyte.omw"
        versionCode 14
        versionName calculateVersion()

        minSdkVersion 21
        targetSdkVersion 27

        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    buildTypes {
        release {
            minifyEnabled false
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "omw_${variant.buildType.name}_${defaultConfig.versionName}.apk"
        }
    }
}
repositories {
    jcenter()
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    implementation 'com.github.codekidX:storage-chooser:2.0.4.4'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko-sqlite:$anko_version"

    implementation 'com.bugsnag:bugsnag-android-ndk:4.+'

    testImplementation 'junit:junit:4.12'
}
